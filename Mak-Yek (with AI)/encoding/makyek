% Facts generated by software:
% 	enemy(X,Y).
% 	computer(P,X,Y).
% 	free(X,Y).

spot(0..7,0..7).

% Guess
move(P, X, Y) | notMove(P, X, Y) :- free(X, Y), computer(P, X, _).
move(P, X, Y) | notMove(P, X, Y) :- free(X, Y), computer(P, _, Y).

occupied(X,Y):- spot(X,Y), not free(X,Y).

%Nessuna pedina tra il computer e la destinazione
:- move(P, X, Y), occupied(EX, Y), computer(P, CX, Y), CX<X, EX<X, EX>CX.
:- move(P, X, Y), occupied(EX, Y), computer(P, CX, Y), CX>X, EX>X, EX<CX.
:- move(P, X, Y), occupied(X, EY), computer(P, X, CY), CY<Y, EY<Y, EY>CY.
:- move(P, X, Y), occupied(X, EY), computer(P, X, CY), CY>Y, EY>Y, EY<CY.


%Muovi solo una pedina
:- move(P, _, _), move(P1, _, _), P<>P1.

%Scegli solo e almeno una mossa della pedina 
:-#count{P, X, Y : move(P, X, Y)}!=1.


%Intervention
:~ notMove(P,X,Y), enemy(X+1,Y), enemy(X-1,Y), computer(P,X,CY), CY>Y, #count{QY:occupied(X,QY),QY<CY, QY>=Y}=0. [1@2, P,X,Y]
:~ notMove(P,X,Y), enemy(X+1,Y), enemy(X-1,Y), computer(P,X,CY), CY<Y, #count{QY:occupied(X,QY),QY>CY, QY<=Y}=0. [1@2, P,X,Y]
:~ notMove(P,X,Y), enemy(X,Y+1), enemy(X,Y-1), computer(P,CX,Y), CX>X, #count{QX:occupied(QX,Y),QX<CX, QX>=X}=0. [1@2, P,X,Y]
:~ notMove(P,X,Y), enemy(X,Y+1), enemy(X,Y-1), computer(P,CX,Y), CX<X, #count{QX:occupied(QX,Y),QX>CX, QX<=X}=0. [1@2, P,X,Y]

%Capture
%%Controlla a sinistra
:~ notMove(P,X,Y), enemy(X-1,Y), computer(P2,X-2,Y), computer(P,X,CY), CY<Y, P<>P2, #count{QY:occupied(X,QY),QY>CY, QY<=Y}=0. [1@1, P,X,Y]
:~ notMove(P,X,Y), enemy(X-1,Y), computer(P2,X-2,Y), computer(P,X,CY), CY>Y, P<>P2, #count{QY:occupied(X,QY),QY<CY, QY>=Y}=0. [1@1, P,X,Y]

%%Controlla a destra
:~ notMove(P,X,Y), enemy(X+1,Y), computer(P2, X+2,Y), computer(P,X,CY), CY>Y, P<>P2,#count{QY:occupied(X,QY),QY<CY, QY>=Y}=0. [1@1, P,X,Y]
:~ notMove(P,X,Y), enemy(X+1,Y), computer(P2, X+2,Y), computer(P,X,CY), CY<Y, P<>P2, #count{QY:occupied(X,QY),QY>CY, QY<=Y}=0. [1@1, P,X,Y]

%%Controlla sopra
:~ notMove(P,X,Y), enemy(X,Y-1), computer(P2, X,Y-2), computer(P,CX,Y), CX>X, P<>P2, #count{QX:occupied(QX,Y),QX<CX, QX>=X}=0. [1@1, P,X,Y]
:~ notMove(P,X,Y), enemy(X,Y-1), computer(P2, X,Y-2), computer(P,CX,Y), CX<X, P<>P2, #count{QX:occupied(QX,Y),QX>CX, QX<=X}=0. [1@1, P,X,Y]

%%Controlla sotto
:~ notMove(P,X,Y), enemy(X,Y+1), computer(P2, X,Y+2), computer(P,CX,Y), CX<X,  P<>P2, #count{QX:occupied(QX,Y),QX>CX, QX<=X}=0. [1@1, P,X,Y]
:~ notMove(P,X,Y), enemy(X,Y+1), computer(P2, X,Y+2), computer(P,CX,Y), CX>X, P<>P2, #count{QX:occupied(QX,Y),QX<CX, QX>=X}=0. [1@1, P,X,Y]

%Evita di favorire un Intervento
%%Sotto
:~ move(P,X,Y), computer(P2, X, Y+2), enemy(EX,Y+1), EX>X, P<>P2, #count{QX:occupied(QX,Y+1),QX>X, QX<EX}=0. [1@2, P,X,Y]
:~ move(P,X,Y), computer(P2, X, Y+2), enemy(EX,Y+1), EX<X, P<>P2, #count{QX:occupied(QX,Y+1),QX<X, QX>EX}=0. [1@2, P,X,Y]

%%Sopra
:~ move(P,X,Y), computer(P2, X, Y-2), enemy(EX,Y-1), EX>X, P<>P2, #count{QX:occupied(QX,Y-1),QX>X, QX<EX}=0. [1@2, P,X,Y]
:~ move(P,X,Y), computer(P2, X, Y-2), enemy(EX,Y-1), EX<X, P<>P2, #count{QX:occupied(QX,Y-1),QX<X, QX>EX}=0. [1@2, P,X,Y]

%%Destra 
:~ move(P,X,Y), computer(P2, X+2, Y), enemy(X+1,EY), EY>Y, P<>P2, #count{QY:occupied(X+1,QY),QY>Y, QY<EY}=0. [1@2, P,X,Y]
:~ move(P,X,Y), computer(P2, X+2, Y), enemy(X+1,EY), EY<Y, P<>P2, #count{QY:occupied(X+1,QY),QY<Y, QY>EY}=0. [1@2, P,X,Y]

%%Sinistra
:~ move(P,X,Y), computer(P2, X-2, Y), enemy(X-1,EY), EY>Y, P<>P2, #count{QY:occupied(X-1,QY),QY>Y, QY<EY}=0. [1@2, P,X,Y]
:~ move(P,X,Y), computer(P2, X-2, Y), enemy(X-1,EY), EY>Y, P<>P2, #count{QY:occupied(X-1,QY),QY>Y, QY<EY}=0.[1@2, P,X,Y]